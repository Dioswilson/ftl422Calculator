plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id "org.panteleyev.jpackageplugin" version "1.6.1"
}

group 'com.dioswilson'

mainClassName = 'com.dioswilson.ftl422calculator.FtlCalculatorApplication'

version = "1.0.0"

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.dioswilson.ftl422calculator'
    mainClass = 'com.dioswilson.ftl422calculator.FtlCalculatorApplication'
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.1.2')
    implementation('com.dlsc.formsfx:formsfx-core:11.6.0') {
        exclude(group: 'org.openjfx')
    }
    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

tasks.register("copyDependencies", Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/jmods"
}

tasks.register("copyJar", Copy) {
    from tasks.named("jar")
    into "$buildDir/jmods"
}

tasks.named("jpackage") {
    dependsOn "build", "copyDependencies", "copyJar"

    appName = "ftl422Calculator"
    appVersion = project.version.toString()
    vendor = "Dioswilson"
    runtimeImage = System.getProperty("java.home")
    module = "com.dioswilson.ftl422calculator/com.dioswilson.ftl422calculator.FtlCalculatorApplication"
    modulePaths = [file("$buildDir/jmods")]
    destination = file("$buildDir/dist")
    javaOptions = ["-Dfile.encoding=UTF-8"]
    icon = "icons/FtlCalcIcon.ico"

    windows {
        winMenu = true
        winDirChooser = true
        type = "MSI"
        winShortcut = true
        winShortcutPrompt = true
    }
}
